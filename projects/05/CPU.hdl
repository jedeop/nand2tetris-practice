// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    Mux16(a[0..14]=instruction[0..14], b=aluOut, sel=instruction[15], out=muxAOut);
    Not(in=instruction[15], out=notAOut);
    Or(a=notAOut, b=instruction[5], out=orAOut);
    ARegister(in=muxAOut, out=regAOut, load=orAOut, out[0..14]=addressM);

    Mux16(a=regAOut, b=inM, sel=instruction[12], out=muxAluOut);
    And(a=instruction[15], b=instruction[4], out=andDOut);
    DRegister(in=aluOut, load=andDOut, out=regDOut);

    ALU(x=regDOut, y=muxAluOut, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluOut, zr=aluZr, ng=aluNg, out=outM);

    And(a=instruction[15], b=instruction[3], out=writeM);

    And(a=aluNg, b=instruction[2], out=AndPc1Out);
    And(a=aluZr, b=instruction[1], out=AndPc2Out);
    Or(a=aluZr, b=aluNg, out=orPc0out);
    Not(in=orPc0out, out=notPcOut);
    And(a=notPcOut, b=instruction[0], out=andPc3Out);

    Or(a=AndPc1Out, b=AndPc2Out, out=orPc1Out);
    Or(a=orPc1Out, b=andPc3Out, out=orPc2Out);
    And(a=orPc2Out, b=instruction[15], out=pcFinal);

    PC(in=regAOut, reset=reset, inc=true, out[0..14]=pc, load=pcFinal);

}